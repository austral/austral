import Austral.Memory (
    Pointer,
    Address,
    allocate,
    nullCheck,
    store,
    load,
    deallocate
);


module body Standard.Box is
    -- Mark this module as unsafe so we can use the FFI.
    pragma Unsafe_Module;

    record Box[T: Type]: Linear is
        pointer: Pointer[T];
    end;

    generic [T: Type]
    function box(val: T): Either[Box[T], T] is
        let addr: Address[T] := allocate(sizeof(T));
        case nullCheck(addr) of
            when Some(value: Pointer[T]) do
                store(value, val);
                let box: Box[T] := Box(pointer => value);
                let left: Either[Box[T], T] := Left(left => box);
                return left;
            when None do
                -- Allocation failed, return the object we tried to box.
                let right: Either[Box[T], T] := Right(right => val);
                return right;
        end case;
    end;

    generic [T: Type]
    function unbox(box: Box[T]): T is
        let { pointer: Pointer[T] } := box;
        let value: T := load(pointer);
        deallocate(pointer);
        return value;
    end;

    generic [T: Free, R: Region]
    function readFree(boxref: &[Box[T], R]): T is
        let ptrref: &[Pointer[T], R] := boxref->pointer;
        let ptr: Pointer[T] := !ptrref;
        return load(ptr);
    end;

    generic [T: Free, R: Region]
    function storeFree(boxref: &![Box[T], R], value: T): Unit is
        let ptrref: &![Pointer[T], R] := boxref->pointer;
        let ptr: Pointer[T] := !(ptrref);
        store(ptr, value);
        return nil;
    end;

    generic [T: Type, R: Region]
    function swap(boxref: &![Box[T], R], new: T): T is
        let ptr: Pointer[T] := !(boxref->pointer);
        let old: T := load(ptr);
        store(ptr, new);
        return old;
    end;
end module body.