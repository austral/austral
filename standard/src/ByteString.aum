--
-- Part of the Austral project, under the Apache License v2.0 with LLVM Exceptions.
-- See LICENSE file for details.
--
-- SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception
--
import Standard.Buffer (
    Buffer,
    allocateEmpty,
    initialize,
    destroyFree,
    length as bufLength,
    nth as bufNth,
    storeNth as bufStoreNth
);

module body Standard.ByteString is
    record ByteString: Linear is
        buf: Buffer[Nat8];
    end;

    ---
    --- Lifecycle
    ---

    function makeEmpty(): ByteString is
        let buf: Buffer[Nat8] := allocateEmpty();
        return ByteString(
            buf => buf
        );
    end;

    function makeByteString(size: Index, initial: Nat8): ByteString is
        let buf: Buffer[Nat8] := initialize(size, initial);
        return ByteString(
            buf => buf
        );
    end;

    function fromLiteral(arr: FixedArray[Nat8]): ByteString is
        let size: Index := fixedArraySize(arr);
        let str: ByteString := makeByteString(size, ' ');
        for i from 0 to size - 1 do
            storeNth(&!str, i, arr[i]);
        end for;
        return str;
    end;


    function destroyByteString(str: ByteString): Unit is
        let { buf: Buffer[Nat8] } := str;
        destroyFree(buf);
        return nil;
    end;

    ---
    --- Retrieve
    ---

    generic [R: Region]
    function length(string: &[ByteString, R]): Index is
        return bufLength(&(string->buf));
    end;

    generic [R: Region]
    function nth(string: &[ByteString, R], pos: Index): Nat8 is
        return bufNth(&(string->buf), pos);
    end;

    ---
    --- Update
    ---

    """
    Write a byte to the given position in the string.
    """
    generic [R: Region]
    function storeNth(string: &![ByteString, R], pos: Index, byte: Nat8): Unit is
        return bufStoreNth(&(string->buf), pos, byte);
    end;
end module body. 