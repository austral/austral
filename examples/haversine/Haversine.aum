module body Example.Haversine is
    -- FFI stuff.
    pragma Unsafe_Module;

    function Sin(value: Float64): Float64 is
        pragma Foreign_Import(External_Name => "sin");
    end;

    function Cos(value: Float64): Float64 is
        pragma Foreign_Import(External_Name => "cos");
    end;

    function Arcsin(value: Float64): Float64 is
        pragma Foreign_Import(External_Name => "asin");
    end;

    function Sqrt(value: Float64): Float64 is
        pragma Foreign_Import(External_Name => "sqrt");
    end;

    -- Actual code.

    record Position: Free is
        lat: Float64;
        lon: Float64;
    end;

    constant Earth_Radius : Float64 := 6371.0;

    function Great_Circle_Distance(p1: Position, p2: Position): Float64 is
        let a : Float64 := Sin(0.5 * ((p2.lat) - (p1.lat)));
        let b : Float64 := Sin(0.5 * ((p2.lon) - (p1.lon)));
        let c : Float64 := a*a + Cos(p1.lat)*Cos(p2.lat)*b*b;
        return 2.0 * Earth_Radius * Arcsin(Sqrt(c));
    end;

    function main(): ExitCode is
        return ExitSuccess();
    end;
end module body.
